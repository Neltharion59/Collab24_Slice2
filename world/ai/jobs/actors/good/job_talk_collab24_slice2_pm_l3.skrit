/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_andiemus.skrit

	Author(s)	: 	Bartosz Kijanka, Chad Queen

	Purpose		: 	Just stand there and talk at anyone we're told to.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================2500065
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInit$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	m_Target$	= job$.GoalObject;

	SetState Face$;
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		// $$$ read/write variables from the Game Auditor here, and bring up the appropriate dialogs...

		if ( GameAuditor.GetDb.GetBool( "c24_s2_flag_l3_talked_to" ) )
		{
			if ( GameAuditor.GetDb.GetBool( "party_accept_0x2100228" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_accept_have_money" );
				GameAuditor.GetDb.SetBool( "party_accept_0x2100228", false );
			}
			else if ( GameAuditor.GetDb.GetBool( "party_accept_no_money_0x2100228" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_accept_have_no_money" );
				GameAuditor.GetDb.SetBool( "party_accept_no_money_0x2100228", false );
				GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x2100228", true );
			}
			else if ( GameAuditor.GetDb.GetBool( "party_decline_0x2100228" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_reject" );
				GameAuditor.GetDb.SetBool( "party_decline_0x2100228", false );
				GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x2100228", true );
			}
			else if ( GameAuditor.GetDb.GetBool( "party_disband_0x2100228" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_disband" );
				GameAuditor.GetDb.SetBool( "party_disband_0x2100228", false );
				GameAuditor.GetDb.SetBool( "party_disbanded_0x2100228", true );
			}
			else if ( GameAuditor.GetDb.GetBool( "party_disbanded_0x2100228" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_disband_rejoin" );
			}
			else if ( GameAuditor.GetDb.GetBool( "party_allow_rejoin_0x2100228" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_rejoin" );
				GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x2100228", false );
			}
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_join" );
			}
		}
		else
		{
			m_Go$.GetConversation.RSSelectConversation( "collab24_slice2_conversation_pm_l3_quest_start" );
			GameAuditor.GetDb.SetBool( "c24_s2_flag_l3_talked_to", true );
			GameAuditor.GetDb.SetBool( "c24_s2_flag_l3_talk_again", true );
				
			// Enable wishing pond
			scid quest_trigger_scid$ = MakeScid(0x02100E99);
			Go quest_trigger$ = GoDb.FindGoidByScid( quest_trigger_scid$ ).Go;
			SendWorldMessage( WE_REQ_ACTIVATE, quest_trigger$.Goid, quest_trigger$.Goid );
		}

		TryAnim$('talk');
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...
		if ( GameAuditor.GetDb.GetBool( "c24_s2_flag_l3_talk_again" ) )
		{
			GameAuditor.GetDb.SetBool( "c24_s2_flag_l3_talk_again", false );

			// Make sure we talk again
			scid quest_trigger_scid$ = MakeScid(0x02101AF3);
			Go quest_trigger$ = GoDb.FindGoidByScid( quest_trigger_scid$ ).Go;
			SendWorldMessage( WE_REQ_ACTIVATE, quest_trigger$.Goid, quest_trigger$.Goid );
		}

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


